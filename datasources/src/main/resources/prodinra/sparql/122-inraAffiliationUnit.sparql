PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX prodinra: <http://record.prodinra.inra.fr/>

CONSTRUCT {
    ?position a vivo:Position .
    ?position vivo:relates ?x .
    ?position vivo:relates <http://record.prodinra.inra.fr/individual/INRA> .
    <http://record.prodinra.inra.fr/individual/INRA> a foaf:Organization .
    <http://record.prodinra.inra.fr/individual/INRA> rdfs:label "INRA" .
    ?vcard vcard:hasAddress ?address .
    ?address a vcard:Address .
    ?address vcard:streetAddress ?unitLabel .
    ?address vcard:locality ?city .
    ?address vcard:country ?country .
} WHERE {
    ?x prodinra:inraAffiliation ?inra .
    ?x a foaf:Person .
    ?x obo:ARG_2000028 ?vcard .
    ?inra prodinra:unit ?unit .
    ?unit prodinra:name ?unitLabel .
    OPTIONAL {
        ?unit prodinra:city ?city .
    }
    OPTIONAL {
        ?unit prodinra:country ?country .
    }
    BIND(IRI(CONCAT(STR(?x), "-position-INRA")) AS ?position)
    BIND(IRI(CONCAT(STR(?x), "-vcard-address")) AS ?address)
}
