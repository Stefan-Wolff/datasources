PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX rcuk:     <http://gtr.ukri.org/gtr/api/>
PREFIX rcukp:    <http://gtr.ukri.org/gtr/api/person/>
PREFIX rcuko:    <http://gtr.ukri.org/gtr/api/organisation/>
PREFIX rcukpj:   <http://gtr.ukri.org/gtr/api/project/>
PREFIX rcukf:    <http://gtr.ukri.org/gtr/api/fund/>

CONSTRUCT {
    ?grant vivo:dateTimeInterval ?dti .
    ?project vivo:dateTimeInterval ?dti .
    ?dti a vivo:DateTimeInterval .
    ?dti vivo:start ?dtistart .
    ?dtistart a vivo:DateTimeValue .
    ?dtistart vivo:dateTime ?start .
    ?dtistart vivo:dateTimePrecision vivo:yearMonthDayPrecision .
    ?dti vivo:end ?dtiend .
    ?dtiend a vivo:DateTimeValue .
    ?dtiend vivo:dateTime ?end .
    ?dtiend vivo:dateTimePrecision vivo:yearMonthDayPrecision .
    ?fundingOrg a vivo:FundingOrganization .
    ?grant vivo:assignedBy ?fundingOrg .
    ?fundingOrg vivo:assigns ?grant .
    ?grant vivo:totalAwardAmount ?totalAwardAmount .
} WHERE {
    ?project a vivo:Project .
    ?project rcuk:links ?links .
    ?links rcuk:link ?link .
    ?link rcuk:rel "FUND" .
    ?link rcuk:start ?rcukStart .
    BIND(xsd:dateTime(CONCAT(STRBEFORE(?rcukStart, "T"), "T00:00:00Z")) AS ?start)
    ?link rcuk:end ?rcukEnd .
    BIND(xsd:dateTime(CONCAT(STRBEFORE(?rcukEnd, "T"), "T00:00:00Z")) AS ?end)
    ?grant vivo:fundingVehicleFor ?project .
    ?link vivo:relates ?fund .
    ?funderRelationship vivo:relates ?fund .
    ?funderRelationship rcuk:rel "FUNDER" .
    ?funderRelationship vivo:relates ?fundingOrg .
    FILTER(REGEX(STR(?fundingOrg), "organisations"))
    BIND(IRI(CONCAT(STR(?project), "-dti")) AS ?dti)
    BIND(IRI(CONCAT(STR(?project), "-dti-start")) AS ?dtistart)
    BIND(IRI(CONCAT(STR(?project), "-dti-end")) AS ?dtiend)
    OPTIONAL {
        ?fund rcukf:valuePounds ?valuePounds .
	?valuePounds rcuk:amount ?value .
	BIND(CONCAT(STR(?value), " GBP") AS ?totalAwardAmount)
    }
}
