PREFIX ingest:  <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/>
PREFIX vcard:   <http://www.w3.org/2006/vcard/ns#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX vivo:    <http://vivoweb.org/ontology/core#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo:		<http://purl.obolibrary.org/obo/>



CONSTRUCT {

	# entity "has contact info" 
    ?project obo:ARG_2000028 ?projectVcard .
    ?grant obo:ARG_2000028 ?grantVcard .
    
    ?projectVcard vcard:hasURL ?projectVcardUrl .
    
    ?projectVcardUrl a vcard:URL .
    ?projectVcardUrl vcard:url ?project_url .
    ?projectVcardUrl rdfs:label ?project_url_str .

} WHERE {

    ?project a vivo:Project .
    
    ?grant vivo:fundingVehicleFor ?project .
    
    ?project ingest:websiteurl ?project_url_str .
    
    BIND ( xsd:anyURI( ?project_url_str ) AS ?project_url )
    BIND(IRI(CONCAT(STR(?project), "-vcard")) AS ?projectVcard)
    BIND(IRI(CONCAT(STR(?project), "-vcard-url")) AS ?projectVcardUrl)
    BIND(IRI(CONCAT(STR(?grant), "-vcard")) AS ?grantVcard)
}
