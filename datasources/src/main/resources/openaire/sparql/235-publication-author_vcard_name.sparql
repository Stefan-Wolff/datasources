PREFIX ingest:  <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX obo:     <http://purl.obolibrary.org/obo/>
PREFIX vcard:   <http://www.w3.org/2006/vcard/ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>



CONSTRUCT {

    ?pers obo:ARG_2000028 ?vcard .
    
    ?vcard a vcard:Individual .
    ?vcard vcard:hasName ?name .
    ?name a vcard:Name .
    ?name vcard:familyName ?lastName .
    ?name vcard:givenName ?firstName .
    
} WHERE {

    ?pers a foaf:Person .
    ?pers rdfs:label ?fullname .

    BIND( STRBEFORE( ?fullname, "," ) as ?lastName )
    BIND( STRAFTER( ?fullname, ", " ) as ?firstName )

    BIND(IRI(CONCAT(STR(?pers), "-vcard")) AS ?vcard)
    BIND(IRI(CONCAT(STR(?pers), "-vcard-name")) AS ?name)
}
