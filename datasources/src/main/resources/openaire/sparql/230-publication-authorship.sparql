PREFIX ingest:  <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX bibo:    <http://purl.org/ontology/bibo/>
PREFIX vivo:    <http://vivoweb.org/ontology/core#>



CONSTRUCT {

    ?author a foaf:Person .
    ?author rdfs:label ?fullname .
    
    ?authorship a vivo:Authorship .
    
    ?authorship vivo:rank ?rank .
    ?authorship vivo:relates ?pub .
    ?authorship vivo:relates ?author .

} WHERE {

    ?pub a bibo:Document .
    
    ?pub ingest:rels ?rels .
    ?rels ingest:rel ?rel .
    
    ?rel ingest:ranking ?rank.
    ?rel ingest:fullname ?fullname .

    BIND(IRI(CONCAT(STR(?pub), "-author-", REPLACE(?fullname, "\\W", ""))) AS ?author)
    BIND(IRI(CONCAT(STR(?pub), "-authorship-", REPLACE(?fullname, "\\W", ""))) AS ?authorship)
}
