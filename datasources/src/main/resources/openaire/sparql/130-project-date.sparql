PREFIX ingest:  <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/>
PREFIX vivo:    <http://vivoweb.org/ontology/core#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>



CONSTRUCT {

    ?project vivo:dateTimeInterval ?dti .
    ?grant vivo:dateTimeInterval ?dti .
    
    ?dti a vivo:DateTimeInterval .
    
    ?dti vivo:start ?start .
    ?start a vivo:DateTimeValue .
    ?start vivo:dateTime ?start_date .
    ?start vivo:dateTimePrecision vivo:yearMonthDayPrecision .
    
    ?dti vivo:end ?end .
    ?end a vivo:DateTimeValue .
    ?end vivo:dateTime ?end_date .
    ?end vivo:dateTimePrecision vivo:yearMonthDayPrecision .
    
} WHERE {

    ?project a vivo:Project .
    
    ?grant vivo:fundingVehicleFor ?project .
    
    ?project ingest:startdate ?projStart .
    BIND ( xsd:dateTime( CONCAT ( ?projStart, "T00:00:00Z") ) AS ?start_date)
    
    ?project ingest:enddate ?projEnd .
    BIND ( xsd:dateTime( CONCAT ( ?projEnd, "T00:00:00Z") ) AS ?end_date)

    BIND(IRI(CONCAT(STR(?project), "-dti")) AS ?dti)
    BIND(IRI(CONCAT(STR(?project), "-dti-start")) AS ?start)
    BIND(IRI(CONCAT(STR(?project), "-dti-end")) AS ?end)
}
