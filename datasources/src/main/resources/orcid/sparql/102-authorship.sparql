PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX prodinra: <http://record.prodinra.inra.fr/>
PREFIX orcidwork:   <http://www.orcid.org/ns/work/>
PREFIX orcidcommon: <http://www.orcid.org/ns/common/>
PREFIX abox:   <http://vivo.wheatinitiative.org/individual/>

CONSTRUCT {
    ?authorship a vivo:Authorship .
    ?authorship vivo:rank ?rank .
    ?authorship vivo:relates ?x .
    ?x vivo:relatedBy ?authorship .
    ?authorship vivo:relates ?author .
    ?author a foaf:Person .
    ?author orcidcommon:label ?creditName .
    # preserve the original name in a property that won't be filtered
    # so that we can examine it if necessary
    ?author rdfs:comment ?creditName
} WHERE {
    ?x orcidwork:contributors ?contributors .
    ?contributors orcidwork:contributor ?contributor .
    ?contributor vitro:position ?position .
    ?contributor orcidwork:credit-name ?creditName .
    BIND(xsd:int(?position) AS ?rank)
    BIND(IRI(CONCAT(STR(?x), "-authorship-", REPLACE(STR(?creditName), "\\W", ""))) AS ?authorship)
    BIND(IRI(CONCAT(STR(?x), "-author-", REPLACE(STR(?creditName), "\\W", ""))) AS ?author)
}
