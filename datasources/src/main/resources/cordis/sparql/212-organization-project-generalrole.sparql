PREFIX cordis:	<http://cordis.europa.eu/>
PREFIX vivo:	<http://vivoweb.org/ontology/core#>
PREFIX obo:		<http://purl.obolibrary.org/obo/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>



CONSTRUCT {
	
	?org obo:RO_0000053 ?role .
	# An organization has a (_53) role
	
	?role a obo:BFO_0000023 .
	# That role is a _23 (top level) role
	
	?role obo:BFO_0000054 ?project .
	# This role gets realized (_54) in the project

	?role vivo:identifier ?roleId .
	
} WHERE {
	
	?project a vivo:Project .
	?project vivo:identifier ?projectId .
	
        ?grant vivo:fundingVehicleFor ?project .
	
	?project cordis:relations ?rel .
	?rel cordis:associations ?asc .
	?asc cordis:organization ?org .
	?org vivo:identifier ?orgId .
	
	FILTER NOT EXISTS {
            # If the following path doesn't exist, it means that this organization has no mapped role.
            ?proj vivo:relates ?org .
            ?proj vivo:relates ?someRole .
            ?someRole obo:RO_0000052 ?org .
            ?someRole obo:BFO_0000054 ?proj .
	 }

         BIND(IRI(CONCAT(STR(?project), "-orgRole-", STR(?orgId))) AS ?role)
         BIND(CONCAT(STR(?projectId), "-orgRole-", STR(?orgId)) AS ?roleId)

}
