PREFIX cordis:	<http://cordis.europa.eu/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf:	<http://xmlns.com/foaf/0.1/>
PREFIX vcard:	<http://www.w3.org/2006/vcard/ns#>
PREFIX vivo:	<http://vivoweb.org/ontology/core#>
PREFIX obo:		<http://purl.obolibrary.org/obo/>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>



CONSTRUCT {

	# org "has contact info" 
        ?org obo:ARG_2000028 ?vcard .
    
        ?vcard a vcard:Organization .
        ?vcard vcard:hasAddress ?address .
        ?address vcard:streetAddress ?org_street .
	?address vcard:locality ?org_city .
	?address vcard:postalCode ?org_post .
	?address vivo:Country ?org_country_name .
	
	?vcard vcard:hasURL ?url .
	?url a vcard:URL .
	?url vcard:url ?org_url .
	?url rdfs:label ?org_url_str .
	
} WHERE {

	?org a foaf:Organization .

	?org cordis:address ?adr .
	
	?adr cordis:street ?org_street .
	?adr cordis:city ?org_city .
	?adr cordis:postalCode ?org_post .

        OPTIONAL {
	    ?adr cordis:url ?org_url_str .
	    FILTER(REGEX(STR(?org_url_str), "^http"))
	    BIND ( xsd:anyURI( ?org_url_str ) AS ?org_url )
	    BIND(IRI(CONCAT(STR(?org), "-vcard-url")) AS ?url)
        }
	
        OPTIONAL {
	    ?adr cordis:url ?org_url_str .
	    FILTER(!REGEX(STR(?org_url_str), "^http"))
	    BIND ( xsd:anyURI( CONCAT("http://", STR(?org_url_str)) ) AS ?org_url )
	    BIND(IRI(CONCAT(STR(?org), "-vcard-url")) AS ?url)
        }

	?org cordis:relations ?rel .
	?rel cordis:regions ?reg_pl .
	?reg_pl cordis:region ?reg .
	?reg cordis:name ?org_country_name .
	    
	BIND(IRI(CONCAT(STR(?org), "-vcard")) AS ?vcard)
	BIND(IRI(CONCAT(STR(?org), "-vcard-address")) AS ?address)
}
