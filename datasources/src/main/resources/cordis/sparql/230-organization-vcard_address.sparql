PREFIX cordis:	<http://cordis.europa.eu/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf:	<http://xmlns.com/foaf/0.1/>
PREFIX vcard:	<http://www.w3.org/2006/vcard/ns#>
PREFIX vivo:	<http://vivoweb.org/ontology/core#>
PREFIX obo:		<http://purl.obolibrary.org/obo/>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>



CONSTRUCT {

	# org "has contact info" 
    ?org obo:ARG_2000028 _:vcard .
    
    _:vcard a vcard:Organization .
    _:vcard vcard:hasAddress _:address .
    _:address vcard:streetAddress ?org_street .
	_:address vcard:locality ?org_city .
	_:address vcard:postalCode ?org_post .
	_:address vivo:Country ?org_country_name .
	
	_:vcard vcard:hasURL _:url .
	_:url a vcard:URL .
	_:url vcard:url ?org_url .
	_:url rdfs:label ?org_url_str .
	
} WHERE {

	?org a foaf:Organization .

	?org cordis:address ?adr .
	
	?adr cordis:street ?org_street .
	?adr cordis:city ?org_city .
	?adr cordis:postalCode ?org_post .
	?adr cordis:url ?org_url_str .
	
	BIND ( xsd:anyURI( ?org_url_str ) AS ?org_url )
	
	?org cordis:relations ?rel .
	?rel cordis:regions ?reg_pl .
	?reg_pl cordis:region ?reg .
	?reg cordis:name ?org_country_name .
}
