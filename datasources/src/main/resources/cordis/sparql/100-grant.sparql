PREFIX cordis:	<http://cordis.europa.eu/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX vivo:	<http://vivoweb.org/ontology/core#>
PREFIX obo:		<http://purl.obolibrary.org/obo/>



CONSTRUCT {
	?y a vivo:Project .
	?y rdfs:label ?title .
	?y vivo:description ?objective .
	?y vivo:identifier ?project_id .
	
	_:grant a vivo:Grant .
    _:grant rdfs:label ?title .
    _:grant vivo:description ?objective .
    _:grant vivo:identifier ?project_id .
    _:grant vivo:totalAwardAmount ?project_cost .

	_:grant obo:RO_0000056 ?y .
	# grant "ParticipatesIn" project
    
} WHERE {
	?y a cordis:project .
	?y cordis:acronym ?acronym .
	?y cordis:title ?cordis_title .
	
    BIND( CONCAT( ?acronym, " - ", ?cordis_title ) AS ?title )
    
	OPTIONAL { ?y cordis:objective ?objective }
	
	?y cordis:reference ?project_id .
	?y cordis:totalCost ?project_cost .
}