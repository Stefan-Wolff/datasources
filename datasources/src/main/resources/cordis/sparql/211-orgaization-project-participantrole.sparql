PREFIX cordis:  <http://cordis.europa.eu/>
PREFIX vivo:    <http://vivoweb.org/ontology/core#>
PREFIX obo:     <http://purl.obolibrary.org/obo/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX ingest:  <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>



CONSTRUCT {

    ?grant vivo:relates ?org .
    ?grant vivo:relates ?role .
    
    ?role a vivo:InvestigatorRole .
    ?role obo:RO_0000052 ?org .
    ?role obo:BFO_0000054 ?project .

} WHERE {

    ?project a vivo:Project .
    
    ?grant vivo:fundingVehicleFor ?project .
    
    ?project cordis:relations ?rel .
    ?rel cordis:associations ?asc .
    ?asc cordis:organization ?org .
    
    ?org a foaf:Organization .
    ?org ingest:type "participant" .

    BIND(IRI(CONCAT(STR(?project), "-participant-", STRAFTER(STR(?org), "://"))) AS ?role)
}
