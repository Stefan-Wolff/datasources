PREFIX vivo: <http://vivoweb.org/ontology/core#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

CONSTRUCT {
    ?advisee <http://purl.obolibrary.org/obo/RO_0000053> ?adviseeRole .
    ?adviseeRole <http://purl.obolibrary.org/obo/RO_0000052> ?advisee .
    ?adviseeRole a vivo:AdviseeRole .
    ?adviseeRole vivo:relatedBy ?advisingRelationship .
    ?advisingRelationship vivo:relates ?adviseeRole .
    ?advisingRelationship vivo:relates ?advisee .
    ?advisee vivo:relatedBy ?advisingRelationship .
} WHERE {
    ?advisee vivo:adviseeIn ?advisingRelationship .
    BIND(IRI(CONCAT(STR(?advisingRelationship), "-adviseeRole")) AS ?adviseeRole)
    FILTER NOT EXISTS {
        ?advisee <http://purl.obolibrary.org/obo/RO_0000053> ?adviseeRoleNode .
        ?adviseeRoleNode vivo:relatedBy ?advisingRelationship .
    }
}
